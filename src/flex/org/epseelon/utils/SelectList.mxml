<?xml version="1.0" ?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" preinitialize="initCollections()">
    <mx:Metadata>
        [Event(name="change",type="flash.events.Event")]
    </mx:Metadata>
    <mx:Script>
        <![CDATA[
        import mx.collections.ArrayCollection;
        import mx.events.CollectionEvent;

        [Bindable]
        private var _availableElementsLabel:String;

        public function get availableElementsLabel():String {
            return _availableElementsLabel;
        }

        public function set availableElementsLabel(val:String):void {
            _availableElementsLabel = val;
        }

        [Bindable]
        private var _selectedElementsLabel:String;

        public function get selectedElementsLabel():String {
            return _selectedElementsLabel;
        }

        public function set selectedElementsLabel(val:String):void {
            _selectedElementsLabel = val;
        }

        [Bindable]
        private var _availableElements:ArrayCollection = new ArrayCollection();

        public function set availableElements(val:Array):void {
            _availableElements = new ArrayCollection(val);
            for each(var element:Object in _selectedElements) {
                _availableElements.removeItemAt(_availableElements.getItemIndex(element));
            }
        }

        public function get availableElements():Array {
            return _availableElements.toArray();
        }

        [Bindable]
        private var _selectedElements:ArrayCollection = new ArrayCollection();

        public function set selectedElements(val:Array):void {
            _selectedElements = new ArrayCollection(val);
            for each(var element:Object in _selectedElements) {
                _availableElements.removeItemAt(_availableElements.getItemIndex(element));
            }
            _selectedElements.addEventListener(CollectionEvent.COLLECTION_CHANGE, dispatchChangeEvent);
        }

        public function get selectedElements():Array {
            return _selectedElements.toArray();
        }

        [Bindable]
        private var _labelField:String;

        public function set labelField(val:String):void {
            _labelField = val;
        }

        public function get labelField():String {
            return _labelField;
        }

        private function initCollections():void {
            _selectedElements.addEventListener(CollectionEvent.COLLECTION_CHANGE, dispatchChangeEvent);
        }

        private function dispatchChangeEvent(event:Event):void {
            dispatchEvent(new Event(Event.CHANGE));
        }

        private function addElement():void {
            _selectedElements.addItem(_availableElements.removeItemAt(_availableElements.getItemIndex(availableElementsList.selectedItem)));
        }

        private function removeElement():void {
            _availableElements.addItem(_selectedElements.removeItemAt(_selectedElements.getItemIndex(selectedElementsList.selectedItem)));
        }

        ]]>
    </mx:Script>
    <mx:VBox width="100%" height="100%">
        <mx:Label width="100%" text="{_availableElementsLabel}"/>
        <mx:List id="availableElementsList" width="100%" height="100%" dataProvider="{_availableElements}"
                 labelField="{_labelField}" doubleClickEnabled="true" doubleClick="addElement()"/>
    </mx:VBox>
    <mx:VBox height="100%" verticalAlign="middle">
        <mx:Button icon="@Embed('/icons/arrow_right.png')" enabled="{availableElementsList.selectedItems.length > 0}"
                   click="addElement()"/>
        <mx:Button icon="@Embed('/icons/arrow_left.png')" enabled="{selectedElementsList.selectedItems.length > 0}"
                   click="removeElement()"/>
    </mx:VBox>
    <mx:VBox width="100%" height="100%">
        <mx:Label width="100%" text="{_selectedElementsLabel}"/>
        <mx:List id="selectedElementsList" width="100%" height="100%" dataProvider="{_selectedElements}"
                 labelField="{_labelField}" doubleClickEnabled="true" doubleClick="removeElement()"/>
    </mx:VBox>
</mx:HBox>